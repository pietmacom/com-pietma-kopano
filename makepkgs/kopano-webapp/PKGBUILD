# Maintainer: MartiMcFly <martimcfly [at] autorisation.de>
# Contributor: Archist archist@die-optimisten.net

pkgname='kopano-webapp'
pkgver='4.6.3'
pkgrel=1
pkgdesc='Provides all the familiar email, advanced calendaring and contacts features you need to be productive'
groups=(
    'kopano'
	)
arch=(
    'any'
     )
url='http://www.kopano.com/'
license=('AGPL3')

sourcebranch=$(if [[ "${pkgname}" == *-git ]]; then echo "#branch=master"; else echo "#tag=v${pkgver}"; fi)
source=(
    "${pkgname}::git+https://stash.kopano.io/scm/kw/kopano-webapp.git${sourcebranch}"
	)
md5sums=(
    'SKIP'
	)

makedepends=(
    # WEBAPP: https://stash.kopano.io/projects/KW/repos/kopano-webapp/browse/README.md
    'apache-ant'
    'apache-ant-contrib'
    'gettext'
    'libxml2'

    # PKGBUILD
    'git'
	     )
depends=(
    # WEBAPP: https://stash.kopano.io/projects/KW/repos/kopano-webapp/browse/README.md
    'kopano-core'
    'php'
	)
optdepends=(
    'kopano-webapp-nginx'
	    )

# https://wiki.archlinux.org/index.php/VCS_package_guidelines#Git
pkgver() {
    cd ${srcdir}/${pkgname}
    if [[ "${pkgname}" == *-git ]];
    then
        git tag -l v* --sort=v:refname | tail -n 1 | sed "s|v\(.*\)|\1.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)|"
    else
        echo ${pkgver}
    fi
}

package() {
    cd ${srcdir}/${pkgname}
    make server client DESTDIR="$(realpath ${pkgdir})/usr/share/webapps/${pkgname}"
}